apply from: rootProject.file("gradle-support/spring-boot.gradle")
apply from: rootProject.file("gradle-support/openshift-build.gradle")
apply from: rootProject.file("gradle-support/prepare-docker.gradle")

buildDocker.dependsOn prepareDockerInput
buildDocker.tag = "innovateuk/" + project.name.replaceAll(/ifs-/, "") + ":latest"
buildDocker.inputDir = project.file("$project.buildDir/docker")
tagDocker.imageId = "innovateuk/" + project.name.replaceAll(/ifs-/, "") + ":latest"

afterEvaluate {
    ext {
        dockerDir = file("$project.parent.projectDir/docker/")
    }
}

ext.IFS_WEB_VIRTUALASSISTANT_BOTSECRET = ext.has('ifs.web.virtualAssistant.botSecret') ? ext.get('ifs.web.virtualAssistant.botSecret') : 'authSecretNotConfigured'
ext.IFS_WEB_VIRTUALASSISTANT_BOTID = ext.has('ifs.web.virtualAssistant.botId') ? ext.get('ifs.web.virtualAssistant.botId') : 'ukridev-qnamaker-bot'

group 'innovateuk'
version project.version

apply plugin: 'idea'

project.version = nexusVersion
tagDocker.tag = nexusVersion
osStageRemote.tag = nexusVersion
tagDockerLocal.tag = nexusVersion
osStageLocal.tag = nexusVersion

createContainer.hostName = "application-svc"
createContainer.containerName = "application-svc"
createContainer.containerId = "application-svc"
createContainer.portBindings = ['9001:8000']

pullImageAndCreateContainer.hostName = "application-svc"
pullImageAndCreateContainer.containerName = "application-svc"
pullImageAndCreateContainer.containerId = "application-svc"
pullImageAndCreateContainer.portBindings = ['9001:8000']

def enabled = project.hasProperty('ifs.application-service.enabled') ? project.property('ifs.application-service.enabled').toBoolean() : true
def pullNexusImage = project.hasProperty('ifs.application-service.pull.nexus.image') ? project.property('ifs.application-service.pull.nexus.image').toBoolean() : false
if (enabled && pullNexusImage) {

    [stopNexusContainer, removeNexusContainer, pullImageAndCreateContainer, deployNexusLocal, waitNexusDeploy].each { task ->
        task.enabled = true
    }

    [buildDocker, stop, removeContainer, createContainer, deploy, wait].each { task ->
        task.enabled = false
    }

    assemble.dependsOn deployNexusLocal

} else {
    [buildDocker, stop, removeContainer, createContainer, deploy, wait].each { task ->
        task.enabled = enabled
    }
}

prepareDockerBaseInput {
    inputs.dir file("$rootProject.projectDir/ifs-web-service/ifs-web-core/build/classes")
    inputs.dir file("$rootProject.projectDir/ifs-web-service/ifs-application-commons/build/classes")
    dependsOn ":ifs-web-service:ifs-web-core:build"
    dependsOn ":ifs-web-service:ifs-application-commons:build"
}

dependencies {
    compile project(':ifs-web-service:ifs-web-core')
    compile project(':ifs-web-service:ifs-application-commons')

    implementation("org.springframework.cloud:spring-cloud-starter-bootstrap")

    testCompile project(path: ":ifs-resources", configuration: 'testArtifacts')
    testCompile project(path: ":ifs-commons", configuration: 'testArtifacts')
    testCompile project(path: ":ifs-security", configuration: 'testArtifacts')
    testCompile project(path: ":ifs-web-service:ifs-web-core", configuration: 'testArtifacts')
    testCompile project(path: ":ifs-web-service:ifs-application-commons", configuration: 'testArtifacts')

    testCompile "com.opencsv:opencsv"
}

jar.dependsOn ":ifs-web-service:gulp_default"
processResources.dependsOn ":ifs-web-service:applyProfiles"