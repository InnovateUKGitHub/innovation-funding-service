apply plugin: 'war'

war {
    baseName = 'management'
    version = '0.0.1-SNAPSHOT'
}
configurations {
    providedRuntime
}

dependencies {
    compile project(':ifs-web-service:ifs-web-core')

    compile "org.springframework.boot:spring-boot-starter-thymeleaf"
    providedRuntime "org.springframework.boot:spring-boot-starter-tomcat"
    compile "org.springframework.plugin:spring-plugin-core"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
    compile "com.google.guava:guava:16.0.1"
    compile "commons-io:commons-io:2.4"

    testCompile project(path: ':ifs-data-service', configuration: 'forTesting')
    testCompile project(path: ':ifs-web-service:ifs-web-core', configuration: 'forTesting')

    testCompile "com.openpojo:openpojo:0.8.3"
    testCompile "org.skyscreamer:jsonassert:1.3.0"
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

processResources {
    filesMatching("*.properties") {
        expand project.properties
    }
    if (project.hasProperty("profile") && project.profile == "automated") {
        exclude '**/application.properties'
    }
}

task copyTemplatesInPlace(type: Copy) {
    into "$tomcatWebAppsDir/management/WEB-INF/classes/templates"
    from 'src/main/resources/templates'
}

task copyCssInPlace(type: Copy) {
    into "$tomcatWebAppsDir/management/WEB-INF/classes/static/css"
    from 'src/main/resources/static/css'
}

task copyJsInPlace(type: Copy) {
    into "$tomcatWebAppsDir/management/WEB-INF/classes/static/js/dest"
    from 'src/main/resources/static/js/dest'
}


task deployToTomcat(type: Copy) {
    from war
    into "$tomcatWebAppsDir"
    rename '(.*).war', 'management.war'
}

task cleanDeploy() {
    build.mustRunAfter clean
    deployToTomcat.mustRunAfter build
    dependsOn clean
    dependsOn build
    dependsOn deployToTomcat
}
