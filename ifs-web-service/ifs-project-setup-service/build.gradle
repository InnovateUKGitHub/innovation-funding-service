apply from: rootProject.file("gradle-support/spring-boot.gradle")
apply from: rootProject.file("gradle-support/openshift-build.gradle")
apply from: rootProject.file("gradle-support/prepare-docker.gradle")

ext.dockerDir = "$project.parent.projectDir/docker/"

buildDocker.dependsOn ":docker:app-base-image:buildDocker"
buildDocker.dependsOn prepareDockerInput
buildDocker.tag = "innovateuk/"+project.name.replaceAll(/ifs-/,"")+":latest"
buildDocker.inputDir = project.file("$project.buildDir/docker")
tagDocker.imageId="innovateuk/"+project.name.replaceAll(/ifs-/,"")+":latest"

buildscript {
    ext['thymeleaf.version'] = '3.0.9.RELEASE'

    ext {
        thymeleafVersion = ext['thymeleaf.version']
    }
}

group 'innovateuk'
version project.version

apply plugin: 'idea'

project.version = projectSetupServiceVersion
tagDocker.tag = projectSetupServiceVersion
osStageRemote.tag = projectSetupServiceVersion
tagDockerLocal.tag = projectSetupServiceVersion
osStageLocal.tag = projectSetupServiceVersion

createContainer.hostName="project-setup-svc"
createContainer.containerName="project-setup-svc"
createContainer.containerId="project-setup-svc"
createContainer.portBindings = ['9005:8000']

def enabled = project.hasProperty('ifs.project-setup-service.enabled') ? project.property('ifs.project-setup-service.enabled').toBoolean() : true
[stop, removeContainer, createContainer, deploy, wait].each { task ->
    task.enabled = enabled
}

prepareDockerBaseInput {
    inputs.file("$rootProject.projectDir/ifs-web-service/ifs-web-core/build/classes")
    dependsOn ":ifs-web-service:ifs-web-core:build"
}

ext.serverContext = '/project-setup'

dependencies {
    compile project(':ifs-web-service:ifs-web-core')
    compile project(':ifs-web-service:ifs-project-commons')
    compile project(':ifs-web-service:ifs-application-commons')

    testCompile project (path: ":ifs-resources", configuration: 'testArtifacts')
    testCompile project (path: ":ifs-commons", configuration: 'testArtifacts')
    testCompile project (path: ":ifs-security", configuration: 'testArtifacts')
    testCompile project (path: ":ifs-web-service:ifs-web-core", configuration: 'testArtifacts')
    testCompile project (path: ":ifs-web-service:ifs-application-commons", configuration: 'testArtifacts')
    testCompile project (path: ":ifs-web-service:ifs-project-commons", configuration: 'testArtifacts')

    testCompile "net.sf.opencsv:opencsv:2.3"
}

processResources {
    filesMatching("*.properties") {
        expand project.properties
    }
    if (project.hasProperty("profile") && project.profile == "automated") {
        exclude '**/application.properties'
    }
}

jar.dependsOn ":ifs-web-service:gulp_default"