
configurations {
    providedRuntime
}

jar {
    baseName = 'assessment'
}

//
// Workaround for https://github.com/spring-projects/spring-boot/issues/8308
// Can reinstate excludeDevtools in the bootRepackage task in next release
//
springBoot {
    excludeDevtools = !(project.hasProperty('cloud') && cloud == 'development');
}

bootRepackage {
    mainClass = 'org.innovateuk.ifs.Application'
//    excludeDevtools = !(project.hasProperty('cloud') && cloud == 'development');
}

dependencies {
    compile project(':ifs-web-service:ifs-web-core')

    // weirdly, removing this line from this web module will cause the build to pull in version 2.1.2.RELEASE
    // despite 3.0.1.RELEASE being specified explicitly in ifs-web-core's build.gradle
    compile "org.thymeleaf.extras:thymeleaf-extras-springsecurity4:3.0.1.RELEASE"
    compile "org.thymeleaf.extras:thymeleaf-extras-java8time:3.0.0.RELEASE"

    compile "commons-io:commons-io:2.5"

    testCompile project(':ifs-resources').sourceSets.test.output
    testCompile project(":ifs-commons").sourceSets.test.output
    testCompile project(':ifs-web-service:ifs-web-core').sourceSets.test.output

    testCompile "net.sf.opencsv:opencsv:2.3"
}



processResources {
    filesMatching("*.properties") {
        expand project.properties
    }
}

task buildDocker( type:Docker, dependsOn: build) {
    push = false
    applicationName = 'assessment-service'
    dockerfile = file('src/main/docker/Dockerfile')
    doFirst {
        copy {
            from jar
            into stageDir
        }
        copy {
            from file("${rootProject.projectDir}/setup-files/scripts/docker/coscale-monitoring.sh")
            into stageDir
        }
    }
}
buildDocker.dependsOn prepareDockerFile

