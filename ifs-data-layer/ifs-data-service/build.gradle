def targetBuild = hasProperty('profile') ? profile : 'docker'

apply from: rootProject.file("gradle-support/spring-boot.gradle")
apply from: rootProject.file("gradle-support/asciidoc.gradle")

apply from: rootProject.file("gradle-support/profile-handler.gradle")

processResources.dependsOn applyProfiles

apply from: "../flyway.gradle"

apply from: "db-reset.gradle"
apply from: "db-baseline.gradle"
apply from: "finance-data-service-sync.gradle"

apply from: rootProject.file("gradle-support/openshift-build.gradle")
apply from: rootProject.file("gradle-support/prepare-docker.gradle")
apply from: rootProject.file("gradle-support/mapstruct.gradle")
apply from: rootProject.file("gradle-support/zipkin-support.gradle")

apply plugin: 'idea'

group 'innovateuk'
version project.version

project.version = dataServiceVersion
tagDocker.tag = dataServiceVersion
osStageRemote.tag = dataServiceVersion
tagDockerLocal.tag = dataServiceVersion
osStageLocal.tag = dataServiceVersion

afterEvaluate {
    ext {
        dockerDir = file("$project.parent.projectDir/docker/")
    }
    ext {
        snippetsDir = file('build/generated-snippets')
    }

}
ext.ifsWebSystemUserId = "8394d970-b250-4b15-9621-3534325691b4"

buildDocker.dependsOn prepareDockerInput
buildDocker.dependsOn ":docker:app-base-image:buildDocker"
buildDocker.tag = "innovateuk/" + project.name.replaceAll(/ifs-/, "") + ":latest"
buildDocker.inputDir = project.file("$project.buildDir/docker")
tagDocker.imageId = "innovateuk/" + project.name.replaceAll(/ifs-/, "") + ":latest"


def enabled = project.hasProperty('ifs.data-service.enabled') ? project.property('ifs.data-service.enabled').toBoolean() : true
[stop, removeContainer, createContainer, deploy, wait, flywayClean, flywayMigrate].each { task ->
    task.enabled = enabled
}
deploy.dependsOn ":docker:ifs-mysql:ifs-database:wait"
deploy.mustRunAfter ":docker:ifs-mysql:ifs-database:deploy"

createContainer.portBindings = ['8086:8080','9006:8000']

createContainer.networkAliases=["ifs-data-service"]

ext.idpRestKey = project.properties['idp.rest.key'] ?: "1234567890"
ext.companiesHouseKey = ext.get('ifs.companies-house.key')

ext.companiesHouseLookupEnabled = ext.has('ifs.companies-house.enabled') ? ext.get('ifs.companies-house.enabled') : true
ext.grantProcessFilterByCompetitionIds = ext.has('ifs.grant.process.filterBy.competitionIds') ? ext.get('ifs.grant.process.filterBy.competitionIds') : ""

ext {
    ifsFinanceTotalsBaseURL = 'http://finance-data-service:8080'
}

prepareDockerBaseInput {
    inputs.dir file("$rootProject.projectDir/ifs-data-layer/data-service-commons/build/classes")
    inputs.dir file("$rootProject.projectDir/ifs-data-layer/data-service-notifications/build/classes")
    inputs.dir file("$rootProject.projectDir/ifs-data-layer/data-service-file/build/classes")
    inputs.dir file("$rootProject.projectDir/ifs-data-layer/data-service-alerts/build/classes")
    inputs.dir file("$rootProject.projectDir/ifs-sil-resources/build/classes")
    inputs.dir file("$rootProject.projectDir/ifs-error-util/build/classes")

    dependsOn ":ifs-data-layer:data-service-commons:build"
    dependsOn ":ifs-data-layer:data-service-notifications:build"
    dependsOn ":ifs-data-layer:data-service-file:build"
    dependsOn ":ifs-data-layer:data-service-alerts:build"
    dependsOn ":ifs-sil-resources:build"
    dependsOn ":ifs-error-util:build"

}


dependencies {
    compile project(":ifs-data-layer:data-service-commons")
    compile project(":ifs-data-layer:data-service-notifications")
    compile project(":ifs-data-layer:data-service-file")
    compile project(":ifs-sil-resources")
    compile project(":ifs-error-util")


    compile "org.springframework.boot:spring-boot-starter-data-jpa"
    compile "org.springframework.security:spring-security-crypto"
    compile "org.springframework.boot:spring-boot-starter-security"
    compile "mysql:mysql-connector-java"
    compile "org.springframework:spring-web"
    compile "com.jayway.jsonpath:json-path-assert:2.0.0"
    compile "com.jayway.jsonpath:json-path"
    compile "org.springframework.statemachine:spring-statemachine-core:2.1.3.RELEASE"
    compile 'org.apache.commons:commons-lang3:3.9'
    compile 'org.apache.commons:commons-text:1.3'
    compile 'commons-codec:commons-codec:1.12'
    compile "commons-beanutils:commons-beanutils:1.9.3"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"

    compile 'javax.mail:mail:1.4.7'
    compile "org.apache.httpcomponents:httpasyncclient"
    compile "com.google.guava:guava:27.1-jre"
    compile "commons-io:commons-io:2.5"
    compile 'org.jsoup:jsoup:1.10.3'
    compile "com.opencsv:opencsv:4.3.2"

    testCompile project (path: ":ifs-resources", configuration: 'testArtifacts')
    testCompile project (path: ":ifs-commons", configuration: 'testArtifacts')
    testCompile project (path: ":ifs-security", configuration: 'testArtifacts')
    testCompile project (path: ":ifs-data-layer:data-service-commons", configuration: 'testArtifacts')
    testCompile project (path: ":ifs-data-layer:data-service-file", configuration: 'testArtifacts')
    testCompile project (path: ":ifs-data-layer:data-service-notifications", configuration: 'testArtifacts')
    testCompile project (path: ":ifs-sil-resources", configuration: 'testArtifacts')

    testCompile 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testCompile "com.openpojo:openpojo:0.8.4"
    testCompile "org.skyscreamer:jsonassert:1.3.0"
    testCompile 'au.com.dius:pact-jvm-consumer-junit_2.12:3.5.13'
}

test {

    minHeapSize = "1024m"
    maxHeapSize = "2048m"
    
    finalizedBy asciidoctor

    if (project.hasProperty('excludeTests')) {
        exclude "${project.excludeTests}"
    }

    if (project.hasProperty('testGroups')) {
        systemProperty 'testGroups', project.getProperty('testGroups')
    }

    if (project.hasProperty('pactDir')) {
        systemProperty 'pact.rootDir', project.getProperty('pactDir')
    }


    if (targetBuild == 'docker') {
        dependsOn ":docker:ifs-mysql:ifs-database:initDB"
    }
}


task syncShib(type:Exec) {
    commandLine 'docker', 'exec', 'ldap', '/usr/local/bin/ldap-sync-from-ifs-db.sh'
}

task prepareAnonymisedDB(dependsOn:[":docker:ifs-mysql:ifs-database:updateIfsWebUser", syncShib]) {
    syncShib.mustRunAfter ":docker:ifs-mysql:ifs-database:updateIfsWebUser"
}

flywayClean.dependsOn applyProfiles
flywayMigrate.dependsOn processResources

ext.ifsDataServiceRestBaseURL="http://data-service:8080"
ext.serverPort=8080