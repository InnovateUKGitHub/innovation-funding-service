def targetBuild = hasProperty('profile') ? profile : 'docker'
apply from: targetBuild + "-build.gradle"

apply from: rootProject.file("gradle-support/java-spring-boot.gradle")
apply from: rootProject.file("gradle-support/asciidoc.gradle")

apply from: "../flyway.gradle"

apply from: "db-reset.gradle"
apply from: "db-baseline.gradle"
apply from: "finance-data-service-sync.gradle"

apply from: rootProject.file("gradle-support/openshift-build.gradle")
apply from: rootProject.file("gradle-support/prepare-docker.gradle")
apply from: rootProject.file("gradle-support/mapstruct.gradle")


project.version = dataServiceVersion
tagDocker.tag = dataServiceVersion
osStageRemote.tag = dataServiceVersion
tagDockerLocal.tag = dataServiceVersion
osStageLocal.tag = dataServiceVersion

ext.dockerDir = "$project.parent.projectDir/docker/"

buildDocker.dependsOn prepareDockerInput
buildDocker.dependsOn ":docker:app-base-image:buildDocker"
buildDocker.tag = "innovateuk/" + project.name.replaceAll(/ifs-/, "") + ":latest"
buildDocker.inputDir = project.file("$project.buildDir/docker")
tagDocker.imageId = "innovateuk/" + project.name.replaceAll(/ifs-/, "") + ":latest"


def enabled = project.hasProperty('ifs.data-service.enabled') ? project.property('ifs.data-service.enabled').toBoolean() : true
[stop, removeContainer, createContainer, deploy, wait].each { task ->
    task.enabled = enabled
}
deploy.dependsOn ":docker:ifs-mysql:ifs-database:wait"
deploy.mustRunAfter ":docker:ifs-mysql:ifs-database:deploy"

createContainer.portBindings = ['8086:8080','9006:8000']


createContainer.networkAliases=["ifs-data-service"]

ext.idpRestKey = project.properties['idp.rest.key'] ?: "1234567890"
ext.companyHouseKey = ext.get('ifs.company-house.key')

ext.companiesHouseLookupEnabled = ext.has('ifs.companies-house.enabled') ? ext.get('ifs.companies-house.enabled') : true

bootRun {
    addResources = false
}

ext {
    snippetsDir = file('build/generated-snippets')
    ifsFinanceTotalsBaseURL = 'http://finance-data-service:8080'
}


prepareDockerBaseInput {
    inputs.file("$rootProject.projectDir/ifs-data-layer/data-service-commons/build/classes")
    inputs.file("$rootProject.projectDir/ifs-data-layer/data-service-file/build/classes")
    inputs.file("$rootProject.projectDir/ifs-sil-resources/build/classes")

    dependsOn ":ifs-data-layer:data-service-commons:build"
    dependsOn ":ifs-data-layer:data-service-file:build"
    dependsOn ":ifs-sil-resources:build"
}


dependencies {
    compile project(":ifs-data-layer:data-service-commons")
    compile project(":ifs-data-layer:data-service-file")
    compile project(":ifs-data-layer:data-service-alerts")
    compile project(":ifs-sil-resources")

    compile "org.springframework.boot:spring-boot-starter-data-jpa"
    compile "org.springframework.security:spring-security-crypto"
    compile "org.springframework.boot:spring-boot-starter-security"
    compile "org.springframework.boot:spring-boot-starter-freemarker"
    compile "mysql:mysql-connector-java"
    compile "org.springframework:spring-web"
    compile "com.jayway.jsonpath:json-path-assert:2.0.0"
    compile "com.jayway.jsonpath:json-path"
    compile "org.springframework.statemachine:spring-statemachine-core:1.1.0.RELEASE"
    compile "org.springframework.statemachine:spring-statemachine-recipes-common:1.1.0.RELEASE"
    compile 'org.apache.commons:commons-lang3:3.6'
    compile 'commons-codec:commons-codec:1.10'
    compile "commons-beanutils:commons-beanutils:1.9.3"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
    compile "org.apache.poi:poi:3.14"
    compile "org.apache.poi:poi-ooxml:3.14"

    compile 'javax.mail:mail:1.4.7'
    compile "org.apache.httpcomponents:httpasyncclient"
    compile "com.google.guava:guava:22.0"
    compile "commons-io:commons-io:2.5"
    compile 'org.jsoup:jsoup:1.10.3'
    compile "net.sf.opencsv:opencsv:2.3"
    compile "org.xhtmlrenderer:flying-saucer-core:9.1.6"
    compile "org.xhtmlrenderer:flying-saucer-pdf:9.1.6"

    testCompile project (path: ":ifs-resources", configuration: 'testArtifacts')
    testCompile project (path: ":ifs-commons", configuration: 'testArtifacts')
    testCompile project (path: ":ifs-security", configuration: 'testArtifacts')
    testCompile project (path: ":ifs-data-layer:data-service-commons", configuration: 'testArtifacts')
    testCompile project (path: ":ifs-data-layer:data-service-file", configuration: 'testArtifacts')

    testCompile 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testCompile "com.openpojo:openpojo:0.8.4"
    testCompile "org.skyscreamer:jsonassert:1.3.0"
    testCompile 'org.powermock:powermock-api-mockito:1.6.5'
    testCompile 'org.powermock:powermock-module-junit4:1.6.5'
    testCompile 'au.com.dius:pact-jvm-consumer-junit_2.12:3.5.13'
}

test {
    outputs.dir snippetsDir // why????
    if (project.hasProperty('excludeTests')) {
        exclude "${project.excludeTests}"
    }

    if (project.hasProperty('testGroups')) {
        systemProperty 'testGroups', project.getProperty('testGroups')
    }

    if (project.hasProperty('pactDir')) {
        systemProperty 'pact.rootDir', project.getProperty('pactDir')
    }


    if (targetBuild == 'docker') {
        dependsOn ":docker:ifs-mysql:ifs-database:initDB"
    }
}



task syncShib(type:Exec) {
    commandLine 'docker', 'exec',  '-d', 'ldap', '/usr/local/bin/ldap-sync-from-ifs-db.sh'
}

flywayMigrate.dependsOn processResources
flywayClean.dependsOn processResources

