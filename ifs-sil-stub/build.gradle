apply from: rootProject.file("gradle-support/profile-handler.gradle")
apply from: rootProject.file("gradle-support/spring-boot.gradle")
apply from: rootProject.file("gradle-support/openshift-build.gradle")
apply from: rootProject.file("gradle-support/prepare-docker.gradle")
apply from: rootProject.file("gradle-support/asciidoc.gradle")
apply from: rootProject.file("gradle-support/zipkin-support.gradle")

apply plugin: 'idea'

group 'innovateuk'
version project.version

processResources.dependsOn applyProfiles


dependencies {
    compile project(":ifs-commons")
    compile project(":ifs-sil-resources")
    compile project(":ifs-rest-util")

    compile "org.springframework.boot:spring-boot-starter-web"
    compile 'javax.mail:mail:1.4.7'
    testCompile "org.springframework.restdocs:spring-restdocs-mockmvc:2.0.2.RELEASE"

    testCompile project (path: ":ifs-commons", configuration: 'testArtifacts')
    testCompile project (path: ":ifs-sil-resources", configuration: 'testArtifacts')
    testCompile project (path: ":ifs-resources", configuration: 'testArtifacts')
}

test {
    if (project.hasProperty('excludeTests')) {
        exclude "${project.excludeTests}"
    }

    if (project.hasProperty('testGroups')) {
        systemProperty 'testGroups', project.getProperty('testGroups')
    }
    finalizedBy asciidoctor
}

prepareDockerBaseInput {
    inputs.dir file("$rootProject.projectDir/ifs-commons/build/classes")
    inputs.dir file("$rootProject.projectDir/ifs-sil-resources/build/classes")
    inputs.dir file("$rootProject.projectDir/ifs-security/build/classes")

    dependsOn ":ifs-commons:build"
    dependsOn ":ifs-sil-resources:build"
    dependsOn ":ifs-security:build"
}

buildDocker.dependsOn ":docker:app-base-image:buildDocker"
buildDocker.dependsOn prepareDockerBaseInput

buildDocker.inputDir = project.file("$project.buildDir/docker")

osStageRemote.onlyIf { isNotProductionEnv() }

project.version = silStubVersion
tagDocker.tag = silStubVersion
osStageRemote.tag = silStubVersion
tagDockerLocal.tag = silStubVersion
osStageLocal.tag = silStubVersion

createContainer.hostName = "sil-stub"
createContainer.containerName = "sil-stub"
createContainer.containerId = "sil-stub"
createContainer.portBindings = ['8091:8080']

def enabled = project.hasProperty('initialise') ? project.property('initialise').toBoolean() : false
[stop, removeContainer, createContainer, deploy, wait].each { task ->
    task.enabled = enabled
}

//
// The production application will use the SIL to send emails out.  For non-production environments, the IFS application
// can talk to a SIL stub within the IFS application itself.  The below configuration allows you to configure whether or
// not the stub will attempt to send emails out via an SMTP server, or simply log the emails being sent (it does this
// regardless)
//
ext.ifsSendMailLocally=true
ext.ifsSmtpHost="mail"
ext.ifsSmtpPort=8025
ext.ifsSmtpUser="smtp"
ext.ifsSmtpAuth=false
ext.ifsSmtpPassword="smtp"
ext.ifsSmtpTlsEnabled=false
ext.ifsMailTimeoutMillis=10000

