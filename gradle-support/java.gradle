apply from: rootProject.file("gradle-support/enforcer.gradle")

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'idea'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

dependencies {
    // version must match here and in ifs-libraries repo (bom and java.gradle)
    annotationProcessor "org.projectlombok:lombok:1.18.22"
    testAnnotationProcessor  "org.projectlombok:lombok:1.18.22"
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}
tasks.withType(Test) {
    systemProperty "file.encoding", "UTF-8"
}

jacocoTestReport{
    additionalSourceDirs.from = files(sourceSets.main.allJava.srcDirs)
    reports {
        html.enabled true
        xml.enabled true
        csv.enabled false
        html.destination file("build/reports/jacoco/html")
    }
    executionData.from = files('build/jacoco/test.exec')
}

test {
    beforeTest { desc ->
        println "Executing test ${desc.className}.${desc.name}"
    }

    if (!['ifs-data-service', 'ifs-finance-data-service'].contains(project.name)) {
        maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    }
}

repositories {
    maven { url "https://repo.spring.io/milestone" }
    maven { url "https://plugins.gradle.org/m2/"}
    mavenCentral()
}

jar {
    manifest.attributes provider: 'gradle'
}

compileJava {
    options.compilerArgs += '-parameters'
}

compileTestJava {
    options.compilerArgs += '-parameters'
}

test.finalizedBy jacocoTestReport